startshape START []

shape START
{
	SQ_A [ b 1 sat 1 ]
}

/****************************************************************************
 * SQ - Square
 ***************************************************************************/

shape SQ_A
rule
{
	SQ_B []
}
rule
{
	SQ_B [ r 90 ]
}


shape SQ_B
{
	SQUARE []

	SQ_C []
}

shape SQ_C
rule 0.2
{
	RA_A [ h 180 ]
	RA_A [ r 180 ]
}
rule
{
	SQ_A [ s 0.9 h 180 ]
}

/****************************************************************************
 * RA - Right Angle Triangle
 ***************************************************************************/

shape RA_A
{
	RA_B [ flip 45 ]
}

shape RA_B
{
	RIGHT_ANGLE []

	RA_C [] 
}

shape RA_C
{
	SQ_A [ s 0.5 x 0.25 y -0.25 h 180 ]
}


path RIGHT_ANGLE
{
	LINETO ( 0.5, 0.5 )
	LINETO ( -0.5, -0.5 )
	LINETO ( 0.5, -0.5 )
	LINETO ( 0.5, 0.5 )

	CLOSEPOLY ()
	FILL  () []
}
